#NOP vim:tabstop=2 shiftwidth=2 softtabstop=0 expandtab

#CLASS {RealmOfMagic} {open}
  #SPLIT;

  #SESSION rom rom.mud.de 4000;

  #NOP -- ------------------------------------------------------------------------------------------
  #NOP -- Constants
  #NOP -- ------------------------------------------------------------------------------------------

  #VAR {tickDuration}           {75};

  #MATH {xpMultiplierDuration}  {5 * $tickDuration};
  #MATH {warcryDuration}        {2 * $tickDuration};

  #NOP -- ------------------------------------------------------------------------------------------
  #NOP -- Initial setup
  #NOP -- ------------------------------------------------------------------------------------------

  #VAR {player[age]}            {-1};
  #VAR {player[height]}         {-1};
  #VAR {player[level]}          {-1};
  #VAR {player[tnl]}            {-1};
  #VAR {player[gold]}           {-1};
  #VAR {player[balance]}        {-1};
  #VAR {player[armor-class]}    {-1};
  #VAR {player[position]}       {standing};
  #VAR {player[sex]}            {};
  #VAR {player[alignment]}      {};
  #VAR {player[race]}           {};
  #VAR {player[class]}          {};
  #VAR {player[deaths]}         {-1};
  #VAR {target}                 {};
  #VAR {tickRemaining}          {-1};
  #VAR {xpMultiplierRemaining}  {-1};
  #VAR {warcryRemaining}        {-1};
  #VAR {cooldown[neptun]}       {0};

  #NOP -- ------------------------------------------------------------------------------------------
  #NOP -- Spam
  #NOP -- ------------------------------------------------------------------------------------------

  #SUB {..It emits a faint humming sound!$}  {};
  #SUB {..It has a soft glowing aura!$}      {};
  #SUB {..It glows yellow!}                  {};

  #GAG {^The Messenger of the Realm shouts, 'Don't forget to vote on mudconnect and keep us in the top 10!'};
  #GAG {^http://realmofmagic.org/vote};

  #NOP -- ------------------------------------------------------------------------------------------
  #NOP -- Position
  #NOP -- ------------------------------------------------------------------------------------------

  #ALIAS {_setPosition} {
    #VAR {player[position]} {%1};
  };

  #ACTION {^You are resting.$}                             {_setPosition resting};
  #ACTION {^You are sitting.$}                             {_setPosition sitting};
  #ACTION {^You can't concentrate enough while resting.$}  {_setPosition resting};
  #ACTION {^You go to sleep.$}                             {_setPosition sleeping};
  #ACTION {^You rest your tired bones.$}                   {_setPosition resting};
  #ACTION {^You sit down.$}                                {_setPosition sitting};
  #ACTION {^You sit down and rest your tired bones.$}      {_setPosition resting};
  #ACTION {^You stand up.$}                                {_setPosition standing};
  #ACTION {^You stop resting, and stand up.$}              {_setPosition standing};
  #ACTION {^You wake, and sit up.$}                        {_setPosition sitting};

  #ALIAS {wake} {wak; stand; look};

  #NOP -- ------------------------------------------------------------------------------------------
  #NOP -- Stats
  #NOP -- ------------------------------------------------------------------------------------------

  #ACTION {^You are a %d year old %w %w %w about %* m tall.$} {
    #VAR {player[age]} {%1};
    #VAR {player[sex]} {%2};
    #VAR {player[race]} {%3};
    #VAR {player[class]} {%4};
    #VAR {player[height]} {%5};
  };

  #ACTION {^|%dH %dM %dV} {
    #VAR {player[hp][current]}    {%1};
    #VAR {player[mana][current]}  {%2};
    #VAR {player[mv][current]}    {%3};
  };

  #ACTION {^You have %d(%d) hit, %d(%d) mana and %d(%d) movement points.$} {
    #VAR {player[hp][current]}    {%1};
    #VAR {player[hp][max]}        {%2};
    #VAR {player[mana][current]}  {%3};
    #VAR {player[mana][max]}      {%4};
    #VAR {player[mv][current]}    {%5};
    #VAR {player[mv][max]}        {%6};
  };

  #ACTION {^Strength: %d/%d Intelligence: %d Wisdom: %d Dexterity: %d Constitution: %d$} {
    #VAR {player[str][base]}      {%1};
    #VAR {player[str][extra]}     {%2};
    #VAR {player[int]}            {%3};
    #VAR {player[wis]}            {%4};
    #VAR {player[dex]}            {%5};
    #VAR {player[con]}            {%6};
  };

  #ACTION {^You need %d exp to make level %d.$} {
    #VAR {player[tnl]} {%1};
    #MATH {player[level]} {%2-1};

    #LOCAL {tnl} {};
    #FORMAT {tnl} {%g} {$player[tnl]};
    #SHOWME {You need <028>$tnl<088> exp to make level <028>%2<088>.};
    #LINE GAG;
  };

  #ACTION {^You have %d gold coins.$} {
    #VAR {player[gold]} {%1};

    #LOCAL {gold} {};
    #LOCAL {balance} {};
    #FORMAT {gold} {%g} {$player[gold]};
    #FORMAT {balance} {%g} {$player[balance]};
    #SHOWME {You have <028>$gold<088> (<028>$balance<088>) gold coins.};
    #LINE GAG;
  };

  #ACTION {^You have %d gold coins and your balance is %d.$} {
    #VAR {player[gold]} {%1};
    #VAR {player[balance]} {%2};

    #LOCAL {gold} {};
    #LOCAL {balance} {};
    #FORMAT {gold} {%g} {$player[gold]};
    #FORMAT {balance} {%g} {$player[balance]};
    #SHOWME {You have <028>$gold<088> gold coins and your balance is <028>$balance<088>.};
    #LINE GAG;
  };

  #ACTION {^Your current balance is %d coins.$} {
    #VAR {player[balance]} {%1};

    #LOCAL {balance} {};
    #FORMAT {balance} {%g} {$player[balance]};
    #SHOWME {Your current balance is $balance coins.};
    #LINE GAG;
  };

  #ACTION {^You withdraw %d coins.$} {
    #IF {$player[balance] >= 0} {
      #MATH {player[balance]} {$player[balance] - %1};
    };
  };

  #ACTION {^You deposit %d coins.$} {
    #IF {$player[balance] >= 0} {
      #MATH {player[balance]} {$player[balance] + %1};
    };
  };

  #ACTION {^It should concern you that you have had %d traumatic death experiences so far.$} {
    #VAR {player[deaths]} {%1};
    #SHOWME {It should concern you that you have had <028>$player[deaths]<088> traumatic death experiences so far.};
    #LINE GAG;
  };

  #ACTION {^You are naked and your alignment is '%*'$} {
    #VAR {player[armor-class]} {0};
    #VAR {player[alignment]} {%1};
    #SHOWME {You are <028>naked<088> (<028>$player[armor-class]/10<088>) and your alignment is '<028>$player[alignment]<088>'};
    #LINE GAG;
  };
  #ACTION {^You are almost naked and your alignment is '%*'$} {
    #VAR {player[armor-class]} {1};
    #VAR {player[alignment]} {%1};
    #SHOWME {You are <028>almost naked<088> (<028>$player[armor-class]/10<088>) and your alignment is '<028>$player[alignment]<088>'};
    #LINE GAG;
  };
  #ACTION {^You are almost armored and your alignment is '%*'$} {
    #VAR {player[armor-class]} {2};
    #VAR {player[alignment]} {%1};
    #SHOWME {You are <028>almost armored<088> (<028>$player[armor-class]/10<088>) and your alignment is '<028>$player[alignment]<088>'};
    #LINE GAG;
  };
  #ACTION {^You are slightly armored and your alignment is '%*'$} {
    #VAR {player[armor-class]} {3};
    #VAR {player[alignment]} {%1};
    #SHOWME {You are <028>slightly armored<088> (<028>$player[armor-class]/10<088>) and your alignment is '<028>$player[alignment]<088>'};
    #LINE GAG;
  };
  #ACTION {^You are armored and your alignment is '%*'$} {
    #VAR {player[armor-class]} {4};
    #VAR {player[alignment]} {%1};
    #SHOWME {You are <028>armored<088> (<028>$player[armor-class]/10<088>) and your alignment is '<028>$player[alignment]<088>'};
    #LINE GAG;
  };
  #ACTION {^You are completely armored and your alignment is '%*'$} {
    #VAR {player[armor-class]} {5};
    #VAR {player[alignment]} {%1};
    #SHOWME {You are <028>completely armored<088> (<028>$player[armor-class]/10<088>) and your alignment is '<028>$player[alignment]<088>'};
    #LINE GAG;
  };
  #ACTION {^You are heavily armored and your alignment is '%*'$} {
    #VAR {player[armor-class]} {6};
    #VAR {player[alignment]} {%1};
    #SHOWME {You are <028>heavily armored<088> (<028>$player[armor-class]/10<088>) and your alignment is '<028>$player[alignment]<088>'};
    #LINE GAG;
  };
  #ACTION {^You are very heavily armored and your alignment is '%*'$} {
    #VAR {player[armor-class]} {7};
    #VAR {player[alignment]} {%1};
    #SHOWME {You are <028>very heavily armored<088> (<028>$player[armor-class]/10<088>) and your alignment is '<028>$player[alignment]<088>'};
    #LINE GAG;
  };
  #ACTION {^You are extremely armored and your alignment is '%*'$} {
    #VAR {player[armor-class]} {8};
    #VAR {player[alignment]} {%1};
    #SHOWME {You are <028>extremely armored<088> (<028>$player[armor-class]/10<088>) and your alignment is '<028>$player[alignment]<088>'};
    #LINE GAG;
  };
  #ACTION {^You are extremely heavily armored and your alignment is '%*'$} {
    #VAR {player[armor-class]} {9};
    #VAR {player[alignment]} {%1};
    #SHOWME {You are <028>extremely heavily armored<088> (<028>$player[armor-class]/10<088>) and your alignment is '<028>$player[alignment]<088>'};
    #LINE GAG;
  };
  #ACTION {^You are armored like a tank and your alignment is '%*'$} {
    #VAR {player[armor-class]} {10};
    #VAR {player[alignment]} {%1};
    #SHOWME {You are <028>armored like a tank<088> (<028>$player[armor-class]/10<088>) and your alignment is '<028>$player[alignment]<088>'};
    #LINE GAG;
  };

  #SUB {^You are summonable by other players.$} {<118>%0<088>};

  #ALIAS {stats} {#VAR player};


  #NOP -- ------------------------------------------------------------------------------------------
  #NOP -- Group
  #NOP -- ------------------------------------------------------------------------------------------

  #VAR {isGrouped} {0};

  #ALIAS {as} {assist};
  #ALIAS {of %+} {order fol %1};
  #ALIAS {ok %+} {
    setTarget %1;
    order fol kill $target;
  };
  #ALIAS {gk %+ %+} {
    setTarget %1;
    order %2 kill $target;
    assist %2;
    of assist;
  };

  #ACTION {is now a member of your group.$}   {#VAR {isGrouped} {1}};
  #ACTION {^Your group consists of:$}         {#VAR {isGrouped} {1}};
  #ACTION {^You have disbanded the group.$}   {#VAR {isGrouped} {0}};

  #ACTION {^There were %d coins.} {
    #IF {$isGrouped == 1} {split %1};
  };

  #NOP -- ------------------------------------------------------------------------------------------
  #NOP -- Movement
  #NOP -- ------------------------------------------------------------------------------------------

  #ALIAS {od}   {open door};

  #ALIAS {odd}  {od d};
  #ALIAS {ode}  {od e};
  #ALIAS {odn}  {od n};
  #ALIAS {ods}  {od s};
  #ALIAS {odu}  {od u};
  #ALIAS {odw}  {od w};

  #ALIAS {ud}   {unlock door};

  #ALIAS {udd}  {ud d; odd};
  #ALIAS {ude}  {ud e; ode};
  #ALIAS {udn}  {ud n; odn};
  #ALIAS {uds}  {ud s; ods};
  #ALIAS {udu}  {ud u; odu};
  #ALIAS {udw}  {ud w; odw};

  #NOP -- ------------------------------------------------------------------------------------------
  #NOP -- Retry
  #NOP -- ------------------------------------------------------------------------------------------

  #ACTION {^AAAAaaahh... cough, cough... obviously you need more practice!$}   {warcry};
  #ACTION {^As %+ avoids your bash, you topple over and fall to the ground!$}  {stand};
  #ACTION {^Maybe you should get on your feet first?$}                         {stand};
  #ACTION {^PANIC!%sYou couldn't escape!$}                                     {flee};

  #NOP -- ------------------------------------------------------------------------------------------
  #NOP -- React
  #NOP -- ------------------------------------------------------------------------------------------

  #ACTION {^AUW AUW AUW!!! The thorny bushes}   {chop bushes};
  #ACTION {^OUCHIE!!! The thorny bushes}        {chop bushes};
  #ACTION {^Neptun raises an eyebrow.} {
    #IF {$cooldown[neptun] == 0} {
      bow neptun;
      _cooldown neptun 5;
    };
  };

  #NOP -- ------------------------------------------------------------------------------------------
  #NOP -- Combat
  #NOP -- ------------------------------------------------------------------------------------------

  #ALIAS {setTarget} {
    #IF {"%1" != ""} {#VAR {target} {%1}};
  };

  #ALIAS  {b}     {setTarget %1; bas $target};
  #ALIAS  {bash}  {setTarget %1; bas $target};
  #ALIAS  {k}     {setTarget %1; kic $target};
  #ALIAS  {kick}  {setTarget %1; kic $target};
  #ALIAS  {kill}  {setTarget %1; kil $target};

  #ACTION {^AAAHHH!!! CHARGE!!! BANZAIIII!!!$} {
    #MATH {warcryRemaining} {$warcryDuration - ($tickDuration - $tickRemaining)};
  };

  #ACTION {^The holy sword Sikanda stirs in its sheath but does not yet move into your hand...$} {#send};

  #NOP -- ------------------------------------------------------------------------------------------
  #NOP -- Tick activities
  #NOP -- ------------------------------------------------------------------------------------------

  #ALIAS {tick} {
    #IF {"%1" != ""} {#SUB {%0} {[TICK] %0}};

    #VAR {tickRemaining} {$tickDuration};
  };

  #TICK {eachSecond} {
    #IF {$tickRemaining == 0} {tick};
    #ELSEIF {$tickRemaining > 0} {#MATH {tickRemaining} {$tickRemaining - 1}};

    #IF {$xpMultiplierRemaining > 0} {
      #MATH {xpMultiplierRemaining} {$xpMultiplierRemaining - 1};
    };

    #IF {$warcryRemaining > 0} {
      #MATH {warcryRemaining} {$warcryRemaining - 1};
    };

    #ECHO {
      {(TICK:%02d)--(2XP:%03d)--(warcry:%03d)----------------------------------------------} {-2}
    } {$tickRemaining} {$xpMultiplierRemaining} {$warcryRemaining};
  } {1};

  #NOP -- ------------------------------------------------------------------------------------------
  #NOP -- Tick activities (skills)
  #NOP -- ------------------------------------------------------------------------------------------

  #ACTION {^You calm down.$}                                              {tick %0};
  #ACTION {^Your ability to spy just left you.$}                          {tick %0; spy};

  #NOP -- ------------------------------------------------------------------------------------------
  #NOP -- Tick activities (damage)
  #NOP -- ------------------------------------------------------------------------------------------

  #ACTION {^You feel burning poison in your blood,$}                      {tick %0};
  #ACTION {^You feel the pain as your bleeding wound takes its toll...$}  {tick %0};
  #ACTION {^You suffer tremendous pain.$}                                 {tick %0};

  #NOP -- ------------------------------------------------------------------------------------------
  #NOP -- Tick activities (weather)
  #NOP -- ------------------------------------------------------------------------------------------

  #ACTION {^Darkness once again fills the realm as the moon sets.$}       {tick %0};
  #ACTION {^It is bitterly cold outside.$}                                {tick %0};
  #NOP #ACTION {^It starts to rain.$}                                     {tick %0};
  #ACTION {^It starts to snow.$}                                          {tick %0};
  #ACTION {^Moonlight begins to fill the room.$}                          {tick %0};
  #ACTION {^The blizzard has died down, but the snow continues.$}         {tick %0};
  #ACTION {^The blizzard is over, but it is still snowing.$}              {tick %0};
  #ACTION {^The clouds disappear.$}                                       {tick %0};
  #ACTION {^The day has begun spreading light throughout the land.$}      {tick %0};
  #ACTION {^The flowers start to bloom.$}                                 {tick %0};
  #ACTION {^The full moon begins to rise from the western horizon.$}      {tick %0};
  #ACTION {^The lightning has gone, but it is still raining.$}            {tick %0};
  #ACTION {^The moon slowly sets.$}                                       {tick %0};
  #ACTION {^The night has begun, dropping a blanket of darkness.$}        {tick %0};
  #ACTION {^The night has fallen outside.$}                               {tick %0};
  #ACTION {^The rainstorm slowly dies down.$}                             {tick %0};
  #ACTION {^The sky starts to get cloudy.$}                               {tick %0};
  #ACTION {^The snow has stopped.$}                                       {tick %0};
  #ACTION {^The snowstorm slowly dies down.$}                             {tick %0};
  #ACTION {^The sun begins to rise from the eastern horizon.$}            {tick %0};
  #ACTION {^The sun is directly above, it must be noon.$}                 {tick %0};
  #ACTION {^The sun slowly disappears into the western horizon.$}         {tick %0};
  #ACTION {^The waning moon begins to rise from the western horizon.$}    {tick %0};
  #ACTION {^The waxing moon begins to rise from the western horizon.$}    {tick %0};
  #ACTION {^You are caught in a blizzard.$}                               {tick %0};
  #ACTION {^You are caught in lightning storm.$}                          {tick %0};

  #NOP -- ------------------------------------------------------------------------------------------
  #NOP -- Tick activities (other)
  #NOP -- ------------------------------------------------------------------------------------------

  #ACTION {^You have been idle, and are pulled into a void.$}             {tick %0};
  #ACTION {^A quivering horde of maggots consumes the corpse.$}           {tick %0};

  #ACTION {^{The surroundings seem to slow down, doubling your experience.|You realize though, that this sensation won't last forever.}$} {
    tick %0;
    #VAR {xpMultiplierRemaining} {$xpMultiplierDuration};
  };

  #NOP -- ------------------------------------------------------------------------------------------
  #NOP -- Utilities
  #NOP -- ------------------------------------------------------------------------------------------

  #ALIAS {_cooldown} {
    #VAR {cooldown[%1]} {%2};
    #TICK {cooldown-%1} {
      #MATH {cooldown[%1]} {$cooldown[%1] - 1};
      #IF {$cooldown[%1] == 0} {#UNTICK {cooldown-%1}};
    } {1};
  };

  #ALIAS reload {
    #KILL {all};
    #READ rom.tin;
  };

  #FUNCTION {isEmpty} {
    #IF {"%1" != ""} {#RETURN %1} {#RETURN %2};
  };

  #FUNCTION {ifEmpty} {
    #IF {"%1" != ""} {#RETURN %1} {#RETURN %2};
  };
#CLASS {RealmOfMagic} {close}

#CLASS {RealmOfMagic|Loot} {read} {modules/loot.tin};
#CLASS {RealmOfMagic|Speedwalks} {read} {modules/speedwalks.tin};
#CLASS {RealmOfMagic|Thief} {read} {modules/thief.tin};
#CLASS {RealmOfMagic|AutoTrack} {read} {modules/autotrack.tin};
#CLASS {RealmOfMagic|Alchemy} {read} {modules/alchemy.tin};
#CLASS {RealmOfMagic|Magic} {read} {modules/magic.tin};
#CLASS {RealmOfMagic|Items} {read} {modules/items.tin};
